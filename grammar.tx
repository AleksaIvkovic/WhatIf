//Inventory is when object location is Player

Game: 
    'title' '=' title=STRING
    'start' '=' start=[Location]
    'end' ':' end=Conditions
    'locations' ':' locations+=Location
    'connections' ':' connections+=Connection
    'objects' ':' objects+=Object
    'states' ':' states+=State
    'actions' ':' actions+=Action
;

Location:
    name=ID ':' Description Conditions?
;

Connection: 
    fromL=[Location] direction=Direction toL=[Location]
;

Object:
    name=ID ':' Description 'location' '=' location=[ObjectLocation] 
;

State:
    name=ID ':' 
    'object' '=' related=[ObjectLocation]
    'state' '=' state=BOOL
    'when_true' '=' true_message=STRING 
    'when_false' '=' false_message=STRING
;

Action:
    StateAction | DescribeAction
;

DescribeAction:
    'exam' related=[ObjectLocation] ':' descriptions+=DescribeConditions
;

DescribeConditions:
    ('conditions' ':' conditions+=Condition)? 'message' '=' message=STRING ('print_content' '=' print_content=BOOL)?
;

StateAction:
    name=ID  related=[Object] ':' Conditions? Changes
;

Changes:
    'changes' ':' state_changes+=Change
;

Change:
    StateValueChange | LocationChange
;

LocationChange:
    object=[Object] ':' 'location' '=' location=[ObjectLocation]
;

ObjectLocation:
    Location | Object
;

Conditions:
    'conditions' ':' conditions+=Condition Message?
;

Condition:
    StateCondition | LocationCondition
;

StateCondition:
    state=[State] '==' value=BOOL
;

LocationCondition:
    (object=[Object] ':' 'location' '==' location=[ObjectLocation])
;

Description:
    'description' '=' description=STRING
;

StateValueChange:
    state=[State] '=' value=BOOL
;

Message: 
    'success' '=' success=STRING
    'failure' '=' failure=STRING
;

Direction:
    "N" | "E" | "S" | "W" | "NE" | "NW" | "SE" | "SW"
;