Game: 
    'title' '=' title=STRING
    'intro' '=' intro=STRING
    'start' '=' start=[Location]
    'end' ':' end=GameEnd
    'locations' ':' locations+=Location
    'connections' ':' connections+=Connection
    'objects' ':' objects+=Object
    'states' ':' states+=State
    'actions' ':' actions+=Action
    'verbs' ':' verbs+=Verb
;

GameEnd:
    conditions=Conditions 'success' ':' message=Message
;

Verb:
    name=CID ':' description=Description
;

Location:
    name=CID ':' description=Description (requirements=Conditions 'failure' ':' message=Message)? 
;

Conditions:
    'conditions' ':' conditions+=Condition
;

ConditionWithMessage:
    Condition message=Message
;

Condition:
    StateCondition | LocationCondition 
;

StateCondition:
    state=[State] '==' value=BOOL message=Message?
;

LocationCondition:
    object=[Object] ':' 'location' '==' location=[ObjectLocation] message=Message?
;

ObjectLocation:
    Location | Object
;

Connection: 
    from_location=[Location] direction=Direction to_location=[Location]
;

Object:
    name=CID ':' description=Description? 'location' '=' location=[ObjectLocation] conditions=Conditions? 
;

State:
    name=CID ':' 
    'object' '=' related=[ObjectLocation]
    'state' '=' state=BOOL
    'priority' '=' priority=INT
    ('when_true' '=' true_message=STRING)?
    ('when_false' '=' false_message=STRING)?
;

Change:
    StateValueChange | LocationChange
;

StateValueChange:
    state=[State] '=' value=BOOL
;

LocationChange:
    object=[Object] ':' 'location' '=' location=[ObjectLocation]
;

Action:
    StateAction | DescribeAction 
;

StateAction:
    verb=[Verb]  related=[Object] ':'  conditions=Conditions? 'changes' ':' changes+=Change ('success' ':' message=Message)?
;

DescribeAction:
   'exam'  related=[Object] ':'  
   conditions=Conditions 
   ('print_content' '=' print_content=BOOL)? 
   (message=Message)?
;


Description:
    'description' '=' text=STRING
;

Message: 
    'message' '=' text=STRING
;

Direction:
    "N" | "E" | "S" | "W" | "NE" | "NW" | "SE" | "SW"
;

CID:
    !Keywords ID
;

Keywords:
    'message' | 'description' | 'print_content' | 'exam' | 'changes' | 'success' | 'location' |
    'object' | 'state' | 'priority' | 'when_true' | 'when_false' | 'conditions' | 'failure' | 
    'title'  | 'intro'  | 'start' | 'end' | 'locations' | 'connections' | 'objects' | 'states' | 
    'actions' | 'action_verbs' | 'look' | 'go' | 'save' | 'load' | 'inventory' | 'exit' | 'none' | 'destroyed'
;