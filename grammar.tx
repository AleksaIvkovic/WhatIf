Game: 
    'title' '=' title=STRING
    'start' '=' start=[Location]
    'end' ':' end=Conditions
    'locations' ':' locations+=Location
    'connections' ':' connections+=Connection
    'objects' ':' objects+=Object
    'states' ':' states+=State
    'actions' ':' actions+=Action
    'action_verbs' ':' verbs+=Verb
;

Verb:
    name=ID description=Description
;

Location:
    name=ID ':' description=Description requirements=Conditions?
;

Conditions:
    'conditions' ':' conditions+=Condition message=Message?
;

Condition:
    StateCondition | LocationCondition
;

StateCondition:
    state=[State] '==' value=BOOL
;

LocationCondition:
    object=[Object] ':' 'location' '==' location=[ObjectLocation]
;

ObjectLocation:
    Location | Object
;

Connection: 
    from_location=[Location] direction=Direction to_location=[Location]
;

Object:
    name=ID ':' description=Description? 'location' '=' location=[ObjectLocation] 
;

State:
    name=ID ':' 
    'object' '=' related=[ObjectLocation]
    'state' '=' state=BOOL
    'when_true' '=' true_message=STRING 
    'when_false' '=' false_message=STRING
;

Change:
    StateValueChange | LocationChange
;

StateValueChange:
    state=[State] '=' value=BOOL
;

LocationChange:
    object=[Object] ':' 'location' '=' location=[ObjectLocation]
;

Action:
    verb=[Verb]  related=[Object] ':' execution=ActionType
;

ActionType:
    StateAction | DescribeAction
;

StateAction:
    conditions=Conditions? 'changes' ':' changes+=Change
;

DescribeAction:
    descriptions+=DescribeConditions
;

DescribeConditions:
    conditions=Conditions? ('print_content' '=' print_content=BOOL)?
;

Description:
    'description' '=' text=STRING
;

Message: 
    ('message' '=' text=STRING) | 
    ('success' '=' success=STRING
    'failure' '=' failure=STRING)
;

Direction:
    "N" | "E" | "S" | "W" | "NE" | "NW" | "SE" | "SW"
;