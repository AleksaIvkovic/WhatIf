Game: 
    'title' '=' title=STRING
    'start' '=' start=[Location]
    'end' ':' end=GameEnd
    'locations' ':' locations+=Location
    'connections' ':' connections+=Connection
    'objects' ':' objects+=Object
    'states' ':' states+=State
    'actions' ':' actions+=Action
    'action_verbs' ':' verbs+=Verb
;

GameEnd:
    conditions=Conditions 'success' ':' message=Message
;

Verb:
    name=ID description=Description
;

Location:
    name=ID ':' description=Description (requirements=Conditions 'failure' ':' message=Message)? 
;

Conditions:
    'conditions' ':' conditions+=Condition
;

StateConditions:
    'conditions' ':' conditions+=StateCondition
;

Condition:
    StateCondition | LocationCondition
;

StateCondition:
    state=[State] '==' value=BOOL message=Message?
;

LocationCondition:
    object=[Object] ':' 'location' '==' location=[ObjectLocation] message=Message?
;

ObjectLocation:
    Location | Object
;

Connection: 
    from_location=[Location] direction=Direction to_location=[Location]
;

Object:
    name=ID ':' description=Description? 'location' '=' location=[ObjectLocation] 
;

State:
    name=ID ':' 
    'object' '=' related=[ObjectLocation]
    'state' '=' state=BOOL
    'priority' '=' priority=INT
    ('when_true' '=' true_message=STRING)?
    ('when_false' '=' false_message=STRING)?
;

Change:
    StateValueChange | LocationChange
;

StateValueChange:
    state=[State] '=' value=BOOL
;

LocationChange:
    object=[Object] ':' 'location' '=' location=[ObjectLocation]
;

Action:
    StateAction | DescribeAction 
;

StateAction:
    verb=[Verb]  related=[Object] ':'  conditions=Conditions? 'changes' ':' changes+=Change ('success' ':' message=Message)?
;

DescribeAction:
   'exam'  related=[Object] ':'  
   conditions=StateConditions 
   ('print_content' '=' print_content=BOOL)? 
   (message=Message)?
;

Description:
    'description' '=' text=STRING
;

Message: 
    'message' '=' text=STRING
;

Direction:
    "N" | "E" | "S" | "W" | "NE" | "NW" | "SE" | "SW"
;